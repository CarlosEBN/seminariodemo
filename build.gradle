plugins {
	id 'java'
	id "jacoco"
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.sonarqube' version '4.4.1.3373'
}

sonar {
	properties {
		property "sonar.gradle.skipCompile", "true"
		property "sonar.projectKey", "Mpastrana_demo"
		property "sonar.organization", "mpastrana"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"

		// Excluir solo de la cobertura (pero sí se analizan para bugs/code smells)
		property "sonar.coverage.exclusions", "co/edu/uniajc/controller/**,co/edu/uniajc/model/**,co/edu/uniajc/exception/**"
	}
}

group = 'co.edu.uniajc'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
	implementation 'org.projectlombok:lombok'

	runtimeOnly 'org.postgresql:postgresql'
	compileOnly 'org.projectlombok:lombok'

	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		html.required.set(true)
		xml.required.set(true)
		//Si quiero ver el reporte HTML local debo crear una carpeta donde ponerlo
		html.outputLocation.set(layout.buildDirectory.dir("jacocoHtml"))
	}

	//Excluiré del proyecto lo que no quiero que lea la prueba unitaria (solo debe ver service)
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					"co/edu/uniajc/controller/*",
					"co/edu/uniajc/model/*",
					"co/edu/uniajc/exception/*"
			])
		}))
	}
}

jacoco {
	toolVersion = "0.8.11"
}

plugins.withType(JacocoPlugin).configureEach {
	test.finalizedBy 'jacocoTestReport'
}


